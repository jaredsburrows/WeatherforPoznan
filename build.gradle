buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.2"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    }
}

plugins {
    id "com.gradle.build-scan" version "1.0"
    id "net.ltgt.errorprone" version "0.0.8"
    id "com.github.ben-manes.versions" version "0.12.0"
    id "build-dashboard"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

apply plugin: "com.android.application"
apply plugin: "android-apt"

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["failed", "skipped"]
    }
}

buildScan {
    licenseAgreementUrl = "https://gradle.com/terms-of-service"
    licenseAgree = "yes"
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.piotr.weather"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !Boolean.valueOf(System.getenv("TRAVIS"))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude "META-INF/services/javax.annotation.processing.Processor"
    }
}

ext {
    AA_VERSION = "4.0.0"
    SUPPORT_VERSION = "24.0.0"
    ESPRESSO_VERSION = "2.2.2"
}

dependencies {
    apt "org.androidannotations:androidannotations:$AA_VERSION"
    compile "org.androidannotations:androidannotations-api:$AA_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.michaelpardo:activeandroid:3.1.0-SNAPSHOT"
    compile "com.squareup.retrofit:retrofit:1.9.0"
    compile "com.google.code.gson:gson:2.7"
    compile "com.squareup.picasso:picasso:2.5.2"

    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.1"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"

    androidTestCompile "com.android.support:support-annotations:$SUPPORT_VERSION"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$ESPRESSO_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }
}
